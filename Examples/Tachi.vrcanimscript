# Tachi.vrcanimscript

avatar Tachi

rootobject "jacob-ovrick-tachi-ik (1)"

saved param Wrench: bool = false
saved param FloatAndSplat: bool = false
saved param Geddan: bool = false
saved param Pants: bool = true
saved param TailSwish: bool = false
saved param Hoverboard: bool = false
saved param EyeClosedAmount: float = 0.0
param GroundDistance: float = 0.0

menuitem Pants
    icon symbol_heart
    controls Pants
end
menuitem TailSwish
    icon symbol_paw
    controls TailSwish
end
menuitem "Eyes Closed"
    controls EyeClosedAmount
end
menuitem Hoverboard
    icon symbol_magic
    controls Hoverboard
end
menu Emotes
    menuitem "ゲッダン"
        icon person_dance
        controls Geddan
    end
    menuitem "Float & Splat"
        icon hand_waving
        controls FloatAndSplat
    end
end
menu Broken
    menuitem Wrench
        icon item_sword
        controls Wrench
    end
end

controller base
    extends vrc_AvatarV3LocomotionLayer
    layer Hoverboard
        initial state HoverboardOff
            play "Armature|HoverboardOff (1)" noblendshapes
            goto HoverboardOn if Hoverboard && GroundDistance < 0.5
        state HoverboardOn
            play "Armature|Hoverboarding" noblendshapes
            untracked LeftFoot, RightFoot, Hip
            goto HoverboardOff if !(Hoverboard && GroundDistance < 0.5)

controller action
    layer Action
        initial state Idle
            goto Geddan if Geddan
            goto FloatAndSplat if FloatAndSplat
        state Geddan
            play BaileGETDOWN
            goto Idle if !Geddan
        state FloatAndSplat
            play "Float & Splat"
            goto Idle if !FloatAndSplat

controller fx
    layer Wrench
        initial state WrenchOff
            do
                Wrench::scale = vec3(0, 0, 0)
            goto WrenchOn if Wrench
        state WrenchOn
            do
                Wrench::scale = vec3(1, 1, 1)
            goto WrenchOff if !Wrench

    layer Pants
        initial state PantsOn
            do
                Body::materials[6] = (Material)TachiHidden
                Body::materials[7] = (Material)"T_Tachi_ClothesHair.004"
            goto PantsOff if !Pants
        state PantsOff
            do
                Body::materials[6] = (Material)"T_Tachi_Body.008"
                Body::materials[7] = (Material)TachiHidden
            goto PantsOn if Pants

    additive layer TailSwish
        initial state TailSwishOn
            loop do
                at time 0.
                    Tail_2::rotation.z = 3.25703 curve(-17.9473, -17.9473)
                    Tail_4::rotation.z = -3.63321 curve(-25.7805, -25.7805)
                    Tail_6::rotation.z = -13.5705 curve(-23.8142, -23.8142)
                    Tail_8::rotation.z = -17.5485 curve(4.90953, 4.90953)
                at time 1.13333
                    Tail_2::rotation.z = -9.04968 curve(3.06607, 3.06607)
                    Tail_4::rotation.z = -8.84352 curve(19.4801, 19.4801)
                    Tail_6::rotation.z = -3.06182 curve(36.5213, 36.5213)
                    Tail_8::rotation.z = 10.8043 curve(29.3747, 29.3747)
                at time 2.26667
                    Tail_2::rotation.z = 5.79265 curve(14.8813, 14.8813)
                    Tail_4::rotation.z = 12.4767 curve(6.3003, 6.3003)
                    Tail_6::rotation.z = 16.6323 curve(-12.7071, -12.7071)
                    Tail_8::rotation.z = 6.74419 curve(-34.2843, -34.2843)
                at time 3.4
                    Tail_2::rotation.z = 3.25703 curve(-17.9473, -17.9473)
                    Tail_4::rotation.z = -3.63321 curve(-25.7805, -25.7805)
                    Tail_6::rotation.z = -13.5705 curve(-23.8142, -23.8142)
                    Tail_8::rotation.z = -17.5485 curve(4.90953, 4.90953)
            goto TailSwishOff if !TailSwish
        state TailSwishOff
            do
                Tail_2::rotation.z = 0
                Tail_4::rotation.z = 0
                Tail_6::rotation.z = 0
                Tail_8::rotation.z = 0
            goto TailSwishOn if TailSwish

    layer Hoverboard
        initial state HoverboardOff
            do
                Hoverboard::scale = vec3(0, 0, 0)
            goto HoverboardOn if Hoverboard && GroundDistance < 0.5
        state HoverboardOn
            do
                Hoverboard::scale = vec3(1, 1, 1)
            goto HoverboardOff if !(Hoverboard && GroundDistance < 0.5)

    layer Eyelids
        initial state EyesOpen
            goto EyesPartiallyClosed if EyeClosedAmount > 0
        blend state EyesPartiallyClosed(EyeClosedAmount)
            at value 0
                do
                    Body::blendshapes."eyes-closed" = 0
            at value 1
                do
                    Body::blendshapes."eyes-closed" = 100
            untracked Eyes
            goto EyesOpen if EyeClosedAmount == 0
